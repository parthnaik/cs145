PART I

1. Return the area of Mongolia. 

doc("countries.xml")//country[@name = "Mongolia"]/data(@area)


2. Return the names of all cities that have the same name as the country in which they are located. 

doc("countries.xml")//city[name = parent::country/data(@name)]/name


3. Return the average population of Russian-speaking countries.

avg(doc("countries.xml")//country[language = "Russian"]/data(@population))


4. Return the names of all countries that have at least three cities with population greater than 3 million. 

doc("countries.xml")//country[count(city[population > 3000000]) >= 3]/data(@name)


5. Create a list of French-speaking and German-speaking countries.

let $french := doc("countries.xml")//country[language = "French"]/data(@name)
let $german := doc("countries.xml")//country[language = "German"]/data(@name)
return 
	<result>
		<French>
			{
				for $f in $french
				return <country>{ $f }</country>
			}
		</French>
		<German>
			{
				for $g in $german
				return <country>{ $g }</country>
			}
		</German>
	</result>


6. Return the countries with the highest and lowest population densities. Note that because the "/" operator has its own meaning in XPath and XQuery, the division operator is infix "div". To compute population density use "(@population div @area)". You can assume density values are unique.

let $countries := doc("countries.xml")//country
let $population_densities := $countries/(@population div @area)
let $highest_density := max($population_densities)
let $lowest_density := min($population_densities)
return
	<result>
	  <highest density = "{ $highest_density }">
	    { $countries[(@population div @area) = $highest_density]/data(@name) }
	  </highest>
	  <lowest density = "{ $lowest_density }">
	    { $countries[(@population div @area) = $lowest_density]/data(@name) }
	  </lowest>
	</result>


PART II

1. Return the names of all countries with population greater than 100 million. 

doc("countries.xml")//country[@population > 100000000]/data(@name)


2. Return the names of all countries where over 50% of the population speaks German. (Hint: Depending on your solution, you may want to use ".", which refers to the "current element" within an XPath expression.) 

doc("countries.xml")//country[language[data(.) = "German" and data(@percentage) > 50]]/data(@name)


3. Return the names of all countries where a city in that country contains more than one-third of the country's population. 

doc("countries.xml")//country[city/population > (@population div 3)]/data(@name)


4. Return the population density of Qatar. Note: Since the "/" operator has its own meaning in XPath and XQuery, the division operator is "div". To compute population density use "(@population div @area)". 

doc("countries.xml")//country[@name = "Qatar"]/(@population div @area)


5. Return the names of all countries whose population is less than one thousandth that of some city (in any country).

doc("countries.xml")//country[@population < doc("countries.xml")//city/(population div 1000)]/data(@name)


6. Return all city names that appear more than once, i.e., there is more than one city with that name in the data. Return only one instance of each such city name. (Hint: You might want to use the "preceding" and/or "following" navigation axes for this query, which were not covered in the video or our demo script; they match any preceding or following node, not just siblings.) 

doc("countries.xml")//city[name = preceding::name and not (name = following::name)]/name


7. Return the names of all countries containing a city such that some other country has a city of the same name. (Hint: You might want to use the "preceding" and/or "following" navigation axes for this query, which were not covered in the video or our demo script; they match any preceding or following node, not just siblings.) 

let $countries := doc("countries.xml")//country
for $c in $countries
return $c[city/name = preceding::country/city/name or city/name = following::country/city/name]/data(@name)


8. Return the names of all countries whose name textually contains a language spoken in that country. For instance, Uzbek is spoken in Uzbekistan, so return Uzbekistan. (Hint: You may want to use ".", which refers to the "current element" within an XPath expression.) 

doc("countries.xml")//country[language[contains(parent::country/@name, .)]]/data(@name)


9. Return the names of all countries in which people speak a language whose name textually contains the name of the country. For instance, Japanese is spoken in Japan, so return Japan. (Hint: You may want to use ".", which refers to the "current element" within an XPath expression.) 

doc("countries.xml")//country[language[contains(., parent::country/@name)]]/data(@name)


10. Return all languages spoken in a country whose name textually contains the language name. For instance, German is spoken in Germany, so return German. (Hint: Depending on your solution, may want to use data(.), which returns the text value of the "current element" within an XPath expression.) 

doc("countries.xml")//language[contains(parent::country/@name, .)]/data(.)


11. Return all languages whose name textually contains the name of a country in which the language is spoken. For instance, Icelandic is spoken in Iceland, so return Icelandic. (Hint: Depending on your solution, may want to use data(.), which returns the text value of the "current element" within an XPath expression.) 

doc("countries.xml")//language[contains(., parent::country/@name)]/data(.)


12. Return the number of countries where Russian is spoken. 

count(doc("countries.xml")//country[language = "Russian"])


13. Return the names of all countries for which the data does not include any languages or cities, but the country has more than 10 million people. 

doc("countries.xml")//country[not (language) and not (city) and @population > 10000000]/data(@name)


14. Return the name of the country with the highest population. (Hint: You may need to explicitly cast population numbers as integers with xs:int() to get the correct answer.) 

let $countries := doc("countries.xml")//country
let $highest_population := max($countries/data(@population))
return $countries[@population = $highest_population]/data(@name)


15. Return the name of the country that has the city with the highest population. (Hint: You may need to explicitly cast population numbers as integers with xs:int() to get the correct answer.) 

let $cities := doc("countries.xml")//city
let $countries := doc("countries.xml")//country
let $highest_population := max($cities/population)
return $countries[city/population = $highest_population]/data(@name)


16. Return the average number of languages spoken in countries where Russian is spoken. 

count(doc("countries.xml")//country[language = "Russian"]/language) div count(doc("countries.xml")//country[language = "Russian"])


17. Return all country-language pairs where the language is spoken in the country and the name of the country textually contains the language name.

let $countries := doc("countries.xml")//country
for $c in $countries
return $c[language[contains(parent::country/@name, .)]]/data(@name)

for $country in doc("countries.xml")//country
for $language in $country/language[contains(parent::country/@name, .)]
return
  <country language = "{ data($language) }">
    { $country/data(@name) }
  </country>


18. Return all countries that have at least one city with population greater than 7 million. For each one, return the country name along with the cities greater than 7 million.

for $country in doc("countries.xml")//country[city/population > 7000000]
return
	<country>
		{ $country/@name }
		{
      for $city in $country/city[population > 7000000]
      return
        <big>
          { $city/data(name) }
        </big>
 		}
	</country>


19. Return all countries where at least one language is listed, but the total percentage for all listed languages is less than 90%. Return the country element with its name attribute and its language subelements, but no other attributes or subelements. 

for $country in doc("countries.xml")//country[language][sum(language/@percentage) < 90]
return
  <country>
    { $country/@name }
    {
      for $language in $country/language
      return $language
    }
  </country>


20. Return all countries where at least one language is listed, and every listed language is spoken by less than 20% of the population. Return the country element with its name attribute and its language subelements, but no other attributes or subelements. 

for $country in doc("countries.xml")//country[language][count(language) = count(language[@percentage < 20])]
return 
	<country>
		{ $country/@name }
		{
      for $language in $country/language
      return $language
    }
	</country>


21. Find all situations where one country's most popular language is another country's least popular, and both countries list more than one language. (Hint: You may need to explicitly cast percentages as floating-point numbers with xs:float() to get the correct answer.) 

let $countries := doc("countries.xml")//country[count(language) > 1]
let $most_popular_languages :=
  for $country in $countries
  for $language in $country/language
  where xs:float($language/data(@percentage)) = xs:float(max($country/language/data(@percentage)))
  return $language
let $least_popular_languages :=
  for $country in $countries
  for $language in $country/language
  where xs:float($language/data(@percentage)) = xs:float(min($country/language/data(@percentage)))
  return $language

for $most_popular in $most_popular_languages
for $least_popular in $least_popular_languages
where data($most_popular) = data($least_popular)
return
  <LangPair language="{ data($most_popular) }">
    <MostPopular>{ $most_popular/parent::country/data(@name) }</MostPopular>
    <LeastPopular>{ $least_popular/parent::country/data(@name) }</LeastPopular>
  </LangPair>


22. For each language spoken in one or more countries, create a "language" element with a "name" attribute and one "country" subelement for each country in which the language is spoken. The "country" subelements should have two attributes: the country "name", and "speakers" containing the number of speakers of that language (based on language percentage and the country's population). Order the result by language name, and enclose the entire list in a single "languages" element.

let $languages := doc("countries.xml")//country/language
let $language_names := distinct-values($languages)
return
  <languages>
    {
      for $language_name in $language_names
      order by $language_name
      return
        <language name="{ $language_name }">
          {
            for $language in $languages
            let $speakers := xs:int($language/parent::country/@population * $language/(@percentage div 100))
            let $country_name := $language/parent::country/data(@name)
            where data($language) = $language_name
            return
              <country name="{ $country_name }" speakers="{ $speakers }" />
          }
        </language>
    }
  </languages>